apply plugin: 'java-library'
java {
  sourceCompatibility = JavaVersion.VERSION_1_6
  targetCompatibility = JavaVersion.VERSION_1_6
  withSourcesJar()
}

apply plugin: 'org.jetbrains.kotlin.jvm'
compileKotlin {
  kotlinOptions {
    jvmTarget = '1.8'
    freeCompilerArgs = [
        '-progressive',
        '-Xjvm-default=enable',
    ]
  }
}

dependencies {
  api deps.kotlinStdLib

  testImplementation deps.junit
  testImplementation deps.truth
}

def isReleaseBuild = !version.endsWith('-SNAPSHOT')

apply plugin: 'maven-publish'

publishing {
  publications {
    release(MavenPublication) {
      from components.java

      artifact dokkaJarTaskProvider.get()

      pom {
        name = 'Partition'
        description = 'An API for generating markdown tables'
        url = 'https://github.com/ShaishavGandhi/partition'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'shaishgandhi'
            name = 'Shaishav Gandhi'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/ShaishavGandhi/partition.git'
          developerConnection = 'scm:git:ssh://git@github.com/ShaishavGandhi/partition.git'
          url = 'https://github.com/ShaishavGandhi/partition'
        }
      }
    }
  }
  repositories {
    maven {
      url = isReleaseBuild
          ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
          : 'https://oss.sonatype.org/content/repositories/snapshots/'
      credentials {
        username = project.hasProperty('SONATYPE_NEXUS_USERNAME') ? SONATYPE_NEXUS_USERNAME : ''
        password = project.hasProperty('SONATYPE_NEXUS_PASSWORD') ? SONATYPE_NEXUS_PASSWORD : ''
      }
    }
  }
}

if (isReleaseBuild) {
  apply plugin: 'signing'
  signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications
  }
}

tasks.register('publishSnapshot').configure { task ->
  if (!isReleaseBuild) {
    task.dependsOn(tasks.getByName('publish'))
  }
}